plugins {
    id 'org.springframework.boot' version '2.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'idea'
}

group = 'ru.bulldog.eshop'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11

configurations {
    jaxb
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.aspectj:aspectjweaver:1.9.7'
    implementation 'org.flywaydb:flyway-core:7.14.0'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'com.h2database:h2:1.4.200'

    implementation 'org.glassfish.jaxb:jaxb-runtime'
    implementation 'javax.xml.bind:jaxb-api'
    implementation 'wsdl4j:wsdl4j'

    jaxb 'org.glassfish.jaxb:jaxb-xjc'

    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.4'
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'src/generated/jaxb'
        }
    }
}

task genJaxb {
    ext.sourcesDir = "src/generated/jaxb"
    ext.schema = "src/main/resources/products.xsd"

    outputs.dir sourcesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)

            xjc(destdir: sourcesDir, schema: schema) {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }
        }
    }
}

compileJava.dependsOn genJaxb